// Feather disable all

/// @param sphere
/// @param quad

function BonkSphereCollideQuad(_sphere, _quad)
{
    static _nullReaction = __Bonk().__nullReaction;
    static _reaction     = new __BonkClassReaction();
    
    with(_sphere)
    {
        var _sphereRadius = radius;
        var _sphereX = x;
        var _sphereY = y;
        var _sphereZ = z;
    }
    
    with(_quad)
    {
        //Quad struct is ordered P1, P2, P4 in a clockwise order. P3 is generated by `P2 - (P1 - P4)` a.k.a. `P4 + (P2 - P1)`
        
        var _quadX1 = x1;
        var _quadY1 = y1;
        var _quadZ1 = z1;
        
        var _quadX2 = x2;
        var _quadY2 = y2;
        var _quadZ2 = z2;
        
        var _quadX4 = x3;
        var _quadY4 = y3;
        var _quadZ4 = z3;
        
        var _dX12 = _quadX2 - _quadX1;
        var _dY12 = _quadY2 - _quadY1;
        var _dZ12 = _quadZ2 - _quadZ1;
        
        var _dX41 = _quadX1 - _quadX4;
        var _dY41 = _quadY1 - _quadY4;
        var _dZ41 = _quadZ1 - _quadZ4;
        
        var _normalX = _dZ12*_dY41 - _dY12*_dZ41;
        var _normalY = _dX12*_dZ41 - _dZ12*_dX41;
        var _normalZ = _dY12*_dX41 - _dX12*_dY41;
        
        var _length = sqrt(_normalX*_normalX + _normalY*_normalY + _normalZ*_normalZ);
        if (_length <= 0)
        {
            return _nullReaction;
        }
        
        _normalX /= _length
        _normalY /= _length;
        _normalZ /= _length;
        
        var _quadX3 = _quadX2 - _dX41;
        var _quadY3 = _quadY2 - _dY41;
        var _quadZ3 = _quadZ2 - _dZ41;
        
        var _dX23 = -_dX41;
        var _dY23 = -_dY41;
        var _dZ23 = -_dZ41;
        
        var _dX34 = -_dX12;
        var _dY34 = -_dY12;
        var _dZ34 = -_dZ12;
    }
    
    //Distance from the sphere's centre to the plane
    var _refToPlaneDist = dot_product_3d(_sphereX - _quadX1, _sphereY - _quadY1, _sphereZ - _quadZ1, _normalX, _normalY, _normalZ);
    
    //Early out if the sphere is too far away from the plane
    if ((_refToPlaneDist < -_sphereRadius) || (_refToPlaneDist > _sphereRadius))
    {
        return _nullReaction;
    }
    
    //Point on the plane closest to the sphere's centre
    var _refX = _sphereX - _normalX*_refToPlaneDist;
    var _refY = _sphereY - _normalY*_refToPlaneDist;
    var _refZ = _sphereZ - _normalZ*_refToPlaneDist;
    
    //Check the reference point is on the inner side of the edge 1->2
    //If we fail, these values fall through
    var _tempX = _refX - _quadX1;
    var _tempY = _refY - _quadY1;
    var _tempZ = _refZ - _quadZ1;
    
    var _edgeSqrLen = _dX12*_dX12 + _dY12*_dY12 + _dZ12*_dZ12;
    var _edgeX = _dX12;
    var _edgeY = _dY12;
    var _edgeZ = _dZ12;
    
    if (dot_product_3d(_tempZ*_edgeY - _tempY*_edgeZ,
                       _tempX*_edgeZ - _tempZ*_edgeX,
                       _tempY*_edgeX - _tempX*_edgeY,
                       _normalX, _normalY, _normalZ) > 0)
    {
        //Check the reference point is on the inner side of the edge 2->3
        //If we fail, these values fall through
        var _tempX = _refX - _quadX2;
        var _tempY = _refY - _quadY2;
        var _tempZ = _refZ - _quadZ2;
        
        var _edgeSqrLen = _dX23*_dX23 + _dY23*_dY23 + _dZ23*_dZ23;
        var _edgeX = _dX23;
        var _edgeY = _dY23;
        var _edgeZ = _dZ23;
        
        if (dot_product_3d(_tempZ*_edgeY - _tempY*_edgeZ,
                           _tempX*_edgeZ - _tempZ*_edgeX,
                           _tempY*_edgeX - _tempX*_edgeY,
                           _normalX, _normalY, _normalZ) > 0)
        {
            //Check the reference point is on the inner side of the edge 3->1
            //If we fail, these values fall through
            var _tempX = _refX - _quadX3;
            var _tempY = _refY - _quadY3;
            var _tempZ = _refZ - _quadZ3;
            
            var _edgeSqrLen = _dX34*_dX34 + _dY34*_dY34 + _dZ34*_dZ34;
            var _edgeX = _dX34;
            var _edgeY = _dY34;
            var _edgeZ = _dZ34;
            
            if (dot_product_3d(_tempZ*_edgeY - _tempY*_edgeZ,
                               _tempX*_edgeZ - _tempZ*_edgeX,
                               _tempY*_edgeX - _tempX*_edgeY,
                               _normalX, _normalY, _normalZ) > 0)
            {
                //Check the reference point is on the inner side of the edge 3->1
                //If we fail, these values fall through
                var _tempX = _refX - _quadX4;
                var _tempY = _refY - _quadY4;
                var _tempZ = _refZ - _quadZ4;
                
                var _edgeSqrLen = _dX41*_dX41 + _dY41*_dY41 + _dZ41*_dZ41;
                var _edgeX = _dX41;
                var _edgeY = _dY41;
                var _edgeZ = _dZ41;
                
                if (dot_product_3d(_tempZ*_edgeY - _tempY*_edgeZ,
                                   _tempX*_edgeZ - _tempZ*_edgeX,
                                   _tempY*_edgeX - _tempX*_edgeY,
                                   _normalX, _normalY, _normalZ) > 0)
                {
                    //Reference point is inside the triangle
                    
                    with(_reaction)
                    {
                        var _pushLength = sign(_refToPlaneDist) * (_sphereRadius - abs(_refToPlaneDist));
                        dX = _pushLength*_normalX;
                        dY = _pushLength*_normalY;
                        dZ = _pushLength*_normalZ;
                    }
                    
                    return _reaction;
                }
            }
        }
    }
    
    //Catch reference point that is outside the triangle
    
    //Calculate the direction to push the reference point away from the triangle. This is the perpendicular
    //vector from the edge to the reference point
    var _dot = clamp(dot_product_3d(_edgeX, _edgeY, _edgeZ, _tempX, _tempY, _tempZ) / _edgeSqrLen, 0, 1);
    var _pushX = _sphereX - (_refX - _tempX + _dot*_edgeX); 
    var _pushY = _sphereY - (_refY - _tempY + _dot*_edgeY);
    var _pushZ = _sphereZ - (_refZ - _tempZ + _dot*_edgeZ);
    
    var _pushLength = point_distance_3d(0, 0, 0, _pushX, _pushY, _pushZ);
    if (_pushLength == 0)
    {
        //TODO - Handle this edge case
        return _nullReaction;
    }
    
    if (_pushLength >= _sphereRadius)
    {
        return _nullReaction;
    }
    
    with(_reaction)
    {
        //Push out just enough so that the surface of the capsule is touching the triangle
        var _coeff = (_sphereRadius - _pushLength) / _pushLength;
        dX = _coeff*_pushX;
        dY = _coeff*_pushY;
        dZ = _coeff*_pushZ;
    }
    
    return _reaction;
}